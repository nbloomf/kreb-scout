[define >doc @func.eval.1 := Macro(doc; [#

[define int @a := Rand(Range(2;6))]#
[define int @b := Rand(Range(2;6) \\ {@a})]#
[define int @c := Rand(Range(2;6) \\ {@a;@b})]#

[define str @p := Format(^int; latex; Poly((@a).x^3; (@b).x; (@c).1))]#

Evaluate the function \#[ f(x) = @p \#] at $x = 2$, $x = 0$, $x = -3$, and $x = 1/2$.

[if (@show.solution) then [#
\textbf{Solution:} We have
\begin{eqnarray*}
f(2) & = & @a(2)^3 + @b(2) + @c = \boxed{[:int: @a*2*2*2 + @b*2 + @c :]} \\
f(0) & = & @a(0)^3 + @b(0) + @c = \boxed{[:int: @c :]} \\
f(-3) & = & @a(-3)^3 + @b(-3) + @c = \boxed{[:int: @a*(neg 3)*(neg 3)*(neg 3) + @b*(neg 3) + @c :]} \\
f\left(\frac{1}{2}\right) & = & @a\left(\frac{1}{2}\right)^3 + @b\left(\frac{1}{2}\right) + @c = \boxed{[:rat: int(@a)*(1/2)*(1/2)*(1/2) + int(@b)*(1/2) + int(@c) :]}
\end{eqnarray*}
] else []
endif]

  ]; bool @show.solution := #f
)]#




[define >doc @func.eval.poly.1 := Macro(doc; [#

[define int @a := Rand(Range(-5;5) \\ {0})]#
[define int @b := Rand(Range(-5;5) \\ {0;@a})]#
[define int @c := Rand(Range(-5;5) \\ {0;@a;@b})]#
[define int @d := Rand(Range(-5;5) \\ {0;@a;@b;@c})]#

[define int @u := Rand({3;2;1;-1;-2;-3})]#
[define int @v := Rand({4;5;-4;-5})]#

[define ^int @q := FromCoefs(x; {@a;@b;@c;@d})]#
[define str @p := Format(^int; latex; @q)]#

Evaluate the function \#[ f(x) = @p \#] at $x = @u$ and $x = @v$.

[if (@show.solution) then [#
\textbf{Solution:} We have
\#[ f(@u) = [:str: Format(^int; latex; EvalPoly(@q; x <- Poly((@u).1))) :] \#] and \#[ f(@v) = [:str: Format(^int; latex; EvalPoly(@q; x <- Poly((@v).1))) :] \#]
] else []
endif]

  ]; bool @show.solution := #f
)]#





[define >doc @func.eval.2 := Macro(doc; [#

[define int @a := Rand(Range(2;6))]#
[define int @b := Rand(Range(2;6))]#
[define int @c := Rand(Range(2;6))]#
[define int @k := Rand(Range(2;6))]#

Evaluate the function \#[ f(x) = \left\{ \begin{array}{ll} @a x - @b & \mathrm{if}\ x \geq @k \\ & \\ \displaystyle\frac{1}{x^2 - @c} & \mathrm{if}\ x < @k \end{array}\right. \#] at $x = 8$, $x = 1$, and $x = -2$.

[if (@show.solution) then [#
\textbf{Solution:}
This is a \emph{piecewise defined} function, so remember that before we can evaluate $f$ at a particular $x$ we have to test $x$ against the guards.

First we'll find $f(8)$. Since $8 \geq @k$, we use the first branch of $f$. So \#[ f(8) = @a \cdot 8 - @b = \boxed{[:int: @a*8 - @b :]}. \#]
Next we'll find $f(1)$. Since $1 < @k$, we use the second branch of $f$. So \#[ f(1) = @a \cdot 1 - @b = \boxed{[:int: @a - @b :]}. \#]
Finally, we'll find $f(-2)$. Since $-2 < @k$, we use the second branch of $f$. So \#[ f(-2) = @a \cdot (-2) - @b = \boxed{[:int: @a*(-2) - @b :]}. \#]
] else []
endif]

  ]; bool @show.solution := #f
)]#



